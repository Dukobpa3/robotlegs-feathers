<?xml version="1.0"?>
<s:Application
    xmlns:fx="http://ns.adobe.com/mxml/2009"
    xmlns:s="library://ns.adobe.com/flex/spark"

    backgroundAlpha="0"
    frameRate="60"
    addedToStage="onAddedToStage(event)"

    applicationDPI="120"
    >

    <fx:Script><![CDATA[
        import com.playtika.zonk.config.DebugConfig;
        import com.playtika.zonk.config.RootConfig;
        import com.playtika.zonk.config.StarlingConfig;
        import com.playtika.zonk.view.StarlingRoot;

        import mx.events.ResizeEvent;

        import robotlegs.bender.bundles.mvcs.MVCSBundle;
        import robotlegs.bender.extensions.contextView.ContextView;
        import robotlegs.bender.framework.api.IContext;
        import robotlegs.bender.framework.impl.Context;
        import robotlegs.extensions.starlingViewMap.StarlingViewMapExtension;

        import starling.core.Starling;


        private var _context:IContext;
        private var _starling:Starling;

        private function onAddedToStage(event:Event):void {
            stage.align = StageAlign.TOP_LEFT;
            stage.scaleMode = StageScaleMode.NO_SCALE;

            _starling = new Starling(StarlingRoot, stage);

            configureContext();
            configureStarling();

            stage.addEventListener(ResizeEvent.RESIZE, onResize);
        }

        private function onResize(event:Event):void {
            // set rectangle dimensions for viewPort:
            var viewPortRectangle:Rectangle = new Rectangle();
            viewPortRectangle.width = stage.stageWidth;
            viewPortRectangle.height = stage.stageHeight;

            // resize the viewport:
            Starling.current.viewPort = viewPortRectangle;

            // assign the new stage width and height:
            _starling.stage.stageWidth = stage.stageWidth;
            _starling.stage.stageHeight = stage.stageHeight;
        }

        private function configureContext():void {
            _context = new Context();
            _context.install(MVCSBundle, StarlingViewMapExtension);
            _context.configure(new ContextView(this), _starling);

            _context.configure(RootConfig);
            _context.configure(DebugConfig);
            _context.configure(StarlingConfig);
        }

        private function configureStarling():void {
            _starling.showStats = true;
            _starling.antiAliasing = 1;
            _starling.start();
        }
        ]]></fx:Script>
</s:Application>
